buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    //apply plugin: 'maven-publish'
    //apply plugin: 'signing'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group 'at.stefangeyer.challonge'
    version '2.2.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    jacoco {
        toolVersion = "0.8.2"
    }
}

subprojects {
    dependencies {
        testCompile group: 'junit', name: 'junit', version: property('junit.version')
        testCompile group: 'org.mockito', name: 'mockito-core', version: property('mockito.version')
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier = 'javadoc'
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("$buildDir/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}